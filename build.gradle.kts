import dev.detekt.gradle.plugin.getSupportedKotlinVersion

plugins {
  alias(libs.plugins.hmpps.spring.boot)
  alias(libs.plugins.kotlin.spring)
  alias(libs.plugins.kotlin.jpa)
  alias(libs.plugins.detekt)
}

detekt {
  config.setFrom("detekt/detekt.yml")
}

dependencies {
  implementation(project(":common"))
  implementation(project(":infrastructure"))
  implementation(project(":query"))
  implementation(project(":mutation"))

  implementation(libs.hmpps.starter)
  implementation(libs.spring.data.jpa)
  // TODO: remove
  implementation(libs.spring.restclient)
  implementation(libs.spring.webclient)
  implementation(libs.spring.flyway)

  implementation(libs.springdoc)
  implementation(libs.javers)

  // Due to use of a spring bom in hmpps-starter we have to force some versions to override them locally
  implementation(enforcedPlatform(libs.postgres))

  testImplementation(libs.hmpps.starter.test)
  testImplementation(libs.hmpps.sqs)
  testImplementation(libs.coroutines.core)
  testImplementation(libs.spring.resttestclient)

  testImplementation(libs.wiremock)
  testImplementation(libs.mockk)
  testImplementation(libs.swagger.parser) {
    exclude(group = "io.swagger.core.v3")
  }
  testImplementation(libs.redisson.boot)

  testImplementation(libs.awaitility)
  testImplementation(libs.webtestclient)
  testImplementation(testFixtures(project(":infrastructure")))
  testImplementation(testFixtures(project(":common")))
  testImplementation(libs.sartestsupport)
}

kotlin {
  jvmToolchain(25)
  compilerOptions {
    freeCompilerArgs.addAll("-Xannotation-default-target=param-property")
  }
}

configurations.detekt {
  resolutionStrategy.eachDependency {
    if (requested.group == "org.jetbrains.kotlin") {
      useVersion(getSupportedKotlinVersion())
    }
  }
}

tasks.register<Copy>("copyGitHooks") {
  description = "Copy git hooks."
  group = "git hooks"
  from("$rootDir/scripts/hooks/pre-push")
  into("$rootDir/.git/hooks/")

  doLast {
    file("$rootDir/.git/hooks/pre-push").setExecutable(true)
  }
}

tasks.register<Exec>("validateFlywayFilenames") {
  description = "Validate Flyway migration filenames"
  group = "verification"
  commandLine("sh", "$rootDir/scripts/validate_flyway_filenames.sh")
}

tasks.compileKotlin {
  dependsOn("copyGitHooks")
}

tasks.named("check") {
  dependsOn("validateFlywayFilenames")
}

allprojects {
  pluginManager.apply("org.owasp.dependencycheck")
  dependencyCheck {
    skipConfigurations.addAll(listOf("detekt", "detektPlugins"))
  }

  tasks.register<Test>("integrationTest") {
    group = "verification"
    testClassesDirs = sourceSets.test.get().output.classesDirs
    classpath = sourceSets.test.get().runtimeClasspath
    useJUnitPlatform {
      includeTags("integration")
    }
  }

  tasks.register<Test>("unitTest") {
    group = "verification"
    testClassesDirs = sourceSets.test.get().output.classesDirs
    classpath = sourceSets.test.get().runtimeClasspath
    useJUnitPlatform {
      excludeTags("integration")
    }
  }
}

tasks.named<Test>("unitTest") {
  finalizedBy("unitTestAggregateReport")
}

val unitTestAggregateReport by tasks.registering(TestReport::class) {
  group = "verification"
  description = "Aggregates unitTest results from root and subprojects"

  destinationDirectory.set(
    layout.buildDirectory.dir("reports/tests/unitTest"),
  )

  val allUnitTestTasks = allprojects.flatMap { project ->
    project.tasks.withType(Test::class)
      .matching { it.name == "unitTest" }
  }

  dependsOn(allUnitTestTasks)
  testResults.from(allUnitTestTasks.map { it.binaryResultsDirectory })
}

subprojects {
  repositories {
    mavenLocal()
    mavenCentral()
  }
  pluginManager.apply("dev.detekt")
  pluginManager.apply("org.jetbrains.kotlin.jvm")
  pluginManager.apply("org.jetbrains.kotlin.plugin.spring")
  pluginManager.apply("org.jetbrains.kotlin.plugin.jpa")
  pluginManager.apply("org.jlleitschuh.gradle.ktlint")

  detekt {
    config.setFrom("../detekt/detekt.yml")
  }

  tasks.withType<Test> {
    useJUnitPlatform()
  }
}
